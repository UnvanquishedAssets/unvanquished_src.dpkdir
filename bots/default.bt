selector
{
	decorator return( STATUS_FAILURE )
	{
		sequence
		{
			condition haveWeapon( WP_HBUILD ) && distanceTo( E_GOAL ) <= 100 && percentHealth( E_GOAL ) < 1.0
			action resetStuckTime
		}
	}

	behavior unstick

	sequence
	{
		condition alertedToEnemy
		selector
		{
			condition haveWeapon( WP_HBUILD ) && ( !buildingIsDamaged || teamateHasWeapon( WP_HBUILD ) )
			{
				selector
				{
					action equip
					action flee
				}
			}

			decorator timer( 1000 )
			{
				selector
				{
					sequence
					{
						condition alertedToEnemy
						condition team == TEAM_ALIENS
						condition ( distanceTo( E_A_OVERMIND ) <= 100 || distanceTo( E_A_BOOSTER ) <= 100 )
						action fight
					}
					sequence
					{
						condition ( healScore <= 0.5 && percentHealth( E_GOAL ) > 0.3 )
						condition cvar("g_bot_heal")
						condition random > 0.3
						action heal
					}
				}
			}

			action fight
		}
	}

	selector
	{
		sequence
		{
			condition team == TEAM_ALIENS
			condition healScore <= 0.25
			condition cvar("g_bot_heal")
			action heal
		}

		sequence
		{
			condition team == TEAM_HUMANS
			condition !haveUpgrade( UP_MEDKIT )
			condition healScore <= 0.25
			condition cvar("g_bot_heal")
			action heal
		}
	}

	condition team == TEAM_ALIENS
	{
		condition ( aliveTime > 1500 )
		{
			action evolve
		}
	}

	sequence
	{
		condition team == TEAM_HUMANS
		condition !teamateHasWeapon( WP_HBUILD )
		condition buildingIsDamaged
		condition cvar("g_bot_repair")
		decorator timer( 50000 )
		{
			selector
			{
				condition !haveWeapon( WP_HBUILD )
				{
					action buy( WP_HBUILD )
				}

				condition haveWeapon( WP_HBUILD )
				{
					action repair
				}
			}
		}
	}

	condition team == TEAM_HUMANS
	{
		action equip
	}

	sequence
	{
		condition cvar("g_bot_rush")
		condition cvar("g_bot_attackStruct") // rushing doesn't make much sense if you are not allowed to attack buildables
		condition baseRushScore > 0.5
		action rush
	}

	action roam
}
