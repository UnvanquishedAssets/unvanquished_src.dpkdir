// Subroutine to try to prevent bots from getting stuck
selector
{
	decorator return( STATUS_FAILURE )
	{
		// protect bots that are repairing from being considered stuck
		sequence
		{
			condition haveWeapon( WP_HBUILD ) && distanceTo( E_GOAL ) <= 100 && percentHealth( E_GOAL ) < 1.0
			action resetStuckTime
		}
	}


	condition(stuckTime > 5000)
	{
		selector
		{
			condition(stuckTime > 20000)
			{
				action suicide
			}

			decorator timer(1500)
			{
				decorator return(STATUS_FAILURE)
				{
					action jump
				}
			}
			condition(stuckTime > 9000)
			{
				action moveInDir(MOVE_FORWARD)
			}
			condition(stuckTime > 7000)
			{
				action moveInDir(MOVE_RIGHT)
			}
			condition(stuckTime > 6000)
			{
				action moveInDir(MOVE_BACKWARD)
			}
			sequence
			{
				action moveInDir(MOVE_LEFT)
				action moveTo(E_SELF) // cancel old target which may be causing the bot to walk into a wall, etc.
			}
		}
	}
}
